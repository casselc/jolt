;; Library-conformance driver: replay third-party Clojure libraries' own
;; clojure.test suites on jolt and compare the tallies against the recorded
;; standings in manifest.edn.
;;
;; The library checkouts are NOT vendored — they are ordinary upstream clones
;; under $JOLT_CONFORMANCE_LIBS (default ../conformance-libraries). This gate is
;; therefore opt-in (`make libconformance`) and skips cleanly when the checkout
;; is absent. What lives in this repo is the recipe (which paths, which deps,
;; which namespaces) and the expected tally, so a jolt change that regresses a
;; library is caught here instead of being noticed months later.
;;
;; Usage:
;;   jolt run test/conformance/libs/run.clj [lib ...]      ; all, or the named ones
;;   JOLT_LIBCONF_REPORT=<file> jolt run ...               ; dump {name tally} edn
(ns lib-conformance-driver
  (:require [clojure.edn :as edn]
            [clojure.string :as str]
            [jolt.process :as p]))

(def ^:private repo-root
  (or (System/getenv "JOLT_REPO_ROOT") (System/getProperty "user.dir")))

(def ^:private here (str repo-root "/test/conformance/libs"))

(def ^:private libs-root
  (or (System/getenv "JOLT_CONFORMANCE_LIBS")
      (str (.getParent (java.io.File. repo-root)) "/conformance-libraries")))

(def ^:private siblings-root
  ;; first-party jolt libraries (xml, time, db, ...) sit beside the jolt checkout
  (or (System/getenv "JOLT_SIBLING_LIBS")
      (.getParent (java.io.File. repo-root))))

;; Absolute: each child runs with :dir set to the library's own root, so a
;; relative JOLT_BIN (what the Makefile passes) would not resolve there.
(def ^:private jolt-bin
  (let [b (or (System/getenv "JOLT_BIN") "target/release/jolt")]
    (if (str/starts-with? b "/") b (str repo-root "/" b))))

;; ---------------------------------------------------------------- paths

(defn- exists? [p] (.exists (java.io.File. p)))

(defn- resolve-path
  "A manifest path is relative to the library's own root unless it starts with
  `@` (relative to the conformance-libraries root), `~` (a first-party jolt
  sibling library), `!` (this repo's root) or `/` (absolute)."
  [lib-root p]
  (cond
    (str/starts-with? p "/") p
    (str/starts-with? p "@") (str libs-root "/" (subs p 1))
    (str/starts-with? p "~") (str siblings-root "/" (subs p 1))
    (str/starts-with? p "!") (str repo-root "/" (subs p 1))
    :else (str lib-root "/" p)))

;; ---------------------------------------------------------------- ns discovery

;; The name in an `ns` form may sit behind metadata — `(ns ^{:doc "..."} the.name`
;; is how several contrib suites are written — so skip any leading ^{...} / ^:kw.
(def ^:private ns-form-re
  #"\(ns\s+(?:\^(?:\{[^}]*\}|[^\s]+)\s+)*([a-zA-Z][^\s\(\)\[\]{},;]*)")

(defn- ns-name-of [file]
  (let [src (slurp file)]
    (when-let [m (re-find ns-form-re src)]
      (second m))))

(defn- test-file?
  "Does this file define tests? Matches `deftest`/`defspec` however it is
  qualified — plenty of suites call it `t/deftest` through an alias rather than
  referring it in."
  [src]
  (boolean (re-find #"\(\s*(?:[\w.\-]+/)?(?:deftest|defspec)[\s\n]" src)))

(defn- walk-files [dir]
  (let [f (java.io.File. dir)]
    (if (.isDirectory f)
      (mapcat walk-files (map str (.listFiles f)))
      [dir])))

(defn- discover-nses
  "Every namespace under `test-paths` that defines tests, by its own ns form."
  [test-dirs]
  (->> test-dirs
       (filter exists?)
       (mapcat walk-files)
       (filter #(or (str/ends-with? % ".clj") (str/ends-with? % ".cljc")))
       (keep (fn [f]
               (let [src (slurp f)]
                 (when (test-file? src) (ns-name-of f)))))
       distinct
       sort))

;; ---------------------------------------------------------------- run one lib

(def ^:private result-re
  #"TOTAL tests=(-?\d+) pass=(-?\d+) fail=(-?\d+) error=(-?\d+) load-fail=(-?\d+)")

(defn- parse-total [out]
  (when-let [m (re-find result-re out)]
    (let [[_ t pa f e lf] m]
      {:tests (parse-long t) :pass (parse-long pa)
       :fail (parse-long f) :error (parse-long e) :load-fail (parse-long lf)})))

(defn- run-lib [{:keys [name root dir paths deps local-deps extra-deps nses exclude-nses
                        preload shims timeout]
                 :as entry}]
  (let [lib-root (str libs-root "/" (or root name))
        srcs (map #(resolve-path lib-root %) (or paths ["src" "test"]))
        ;; A shim stands in for something the library expects from the JVM that jolt
        ;; has no equivalent for — a Java logging backend, a JSON library built on
        ;; Jackson. It goes AHEAD of the library's own sources and of its resolved
        ;; deps so it wins the namespace, which is the whole point: the alternative
        ;; is editing the checkout, and an edit there silently turns the recorded
        ;; tally into a measurement of our own source. Shims live on jolt-owned
        ;; paths (`!` = this repo, `~` = a first-party jolt library), never inside
        ;; a checkout.
        shims (map #(resolve-path lib-root %) (or shims []))
        deps (map #(resolve-path lib-root %) (or deps []))
        ;; A first-party jolt library must go on as a real :local/root dependency,
        ;; not a bare source path: its deps.edn is what declares :jolt/native, and
        ;; without that the shared library it binds is never loaded. jolt-crypto on
        ;; :paths alone found whatever libcrypto the dynamic loader had — on macOS
        ;; that is Apple's BoringSSL, which aborts the process inside EVP_Digest.
        local-deps (map #(resolve-path lib-root %) (or local-deps []))
        test-dirs (map #(resolve-path lib-root %) (:test-paths entry ["test"]))
        cp (vec (concat [(str here "/runner")] shims srcs deps))
        missing (remove exists? (concat cp local-deps))
        nses (remove (set (or exclude-nses []))
                     (or nses (discover-nses test-dirs)))
        timeout-ms (* 1000 (or timeout 300))]
    (cond
      (seq missing) {:status :missing-paths :detail (vec missing)}
      (empty? nses) {:status :no-tests}
      :else
      (let [locals (into {} (map (fn [p] [(symbol "jolt-lang" (.getName (java.io.File. p)))
                                          {:local/root p}])
                                 local-deps))
            all-deps (merge (or extra-deps {}) locals)
            sdeps (cond-> {:paths cp} (seq all-deps) (assoc :deps all-deps))
            cmd (vec (concat [jolt-bin "-Sdeps" (pr-str sdeps)
                              "-m" "lib-conformance-run" (str timeout-ms)
                              (if (seq preload) (str/join "," preload) "-")]
                             nses))
            ;; A suite that opens a file by a project-relative path needs the
            ;; working directory its own build uses — in a multi-module repo that
            ;; is the MODULE root, not the repo root (ring-core reads
            ;; test/ring/assets/…, which only resolves from ring/ring-core).
            ;; JOLT_MAX_HEAP=off: this is a stress harness, not a user
            ;; workload. 0.8.5 gave jolt a heap ceiling defaulting to 25% of
            ;; RAM (the share the JVM's MaxRAMPercentage uses), which is the
            ;; right default for a program but wrong here — malli's suite alone
            ;; has a live set around 2.5GB, so on any machine with under ~10GB
            ;; the ceiling would fail the suite before it could report a tally,
            ;; and the tally is the whole output. A library that needs a bound
            ;; can ask for one; the harness does not impose one.
            r (apply p/sh {:out :string :err :string
                           :dir (if dir (resolve-path lib-root dir) lib-root)
                           :extra-env {"JOLT_NO_USER_DEPS" "1"
                                       "JOLT_MAX_HEAP" "off"}}
                     cmd)
            out (str (:out r) (:err r))]
        (merge {:status :ran :exit (:exit r) :out out :nses (vec nses)}
               (or (parse-total out) {:status :no-result}))))))

;; ---------------------------------------------------------------- reporting

;; A property-based suite draws random cases, so its assertion count drifts run to
;; run — :tolerance is how far the recorded pass/fail/error counts may move before
;; it counts as a regression. It covers error as well as fail because which of the
;; two a generated case lands in is itself unstable: test.chuck's regex suite feeds
;; random patterns to re-pattern, and whether one is rejected or blows up inside
;; the engine moves run to run while the total stays put. Everything without a
;; :tolerance is pinned exactly.
(defn- worse? [{:keys [pass fail error load-fail]} exp tolerance]
  (or (< pass (- (:pass exp 0) (or tolerance 0)))
      (> fail (+ (:fail exp 0) (or tolerance 0)))
      (> error (+ (:error exp 0) (or tolerance 0)))
      (> load-fail (:load-fail exp 0))))

;; worse?'s mirror: a counter that moved the GOOD way by more than the tolerance.
;; Reported as BETTER rather than as a failure, so recording the improvement is a
;; one-line manifest edit.
;;
;; Two things were wrong here (jolt-8a8). Only `pass` was consulted, so a fix that
;; turns failing assertions into absent ones — a load-fail that starts loading, an
;; error the suite stops reaching — moved fail/error/load-fail down without moving
;; pass up and read as plain `ok`. All four are mirrored now.
;;
;; And :tolerance is a SYMMETRIC noise band, which means it suppresses BETTER
;; exactly as it suppresses WORSE. That is deliberate, not the leftover: inside the
;; band a move is a different draw and not a result, so re-recording it would only
;; re-centre the band on whatever the last run happened to generate. test.check
;; (:tolerance 40) came back pass=245 against a recorded 236 and reports ok for
;; that reason. Above the band the move is real and says so; a library without a
;; :tolerance is pinned exactly, so any rise there is BETTER.
(defn- better? [{:keys [pass fail error load-fail]} exp tolerance]
  (let [t (or tolerance 0)]
    (or (> pass (+ (:pass exp 0) t))
        (< fail (- (:fail exp 0) t))
        (< error (- (:error exp 0) t))
        (< load-fail (:load-fail exp 0)))))

(defn- tally-str [{:keys [tests pass fail error load-fail]}]
  (str "tests=" tests " pass=" pass " fail=" fail " error=" error
       (when (and load-fail (pos? load-fail)) (str " load-fail=" load-fail))))

(defn -main [& args]
  (when-not (exists? libs-root)
    (println (str "SKIP: no library checkout at " libs-root
                  " (set JOLT_CONFORMANCE_LIBS)"))
    (System/exit 0))
  (let [manifest (edn/read-string (slurp (str here "/manifest.edn")))
        wanted (set args)
        entries (cond->> (:libs manifest)
                  (seq wanted) (filter #(wanted (:name %))))
        logdir (str repo-root "/target/libconformance")]
    (.mkdirs (java.io.File. logdir))
    (let [rows (doall
                 (for [{:keys [name skip expect tolerance] :as e} entries]
                   (if skip
                     (do (println (format "%-20s SKIP  %s" name skip))
                         {:name name :verdict :skip})
                     (let [r (run-lib e)]
                       (when (:out r)
                         (spit (str logdir "/" name ".log") (:out r)))
                       (case (:status r)
                         :missing-paths
                         (do (println (format "%-20s MISSING  %s" name
                                              (str/join " " (:detail r))))
                             {:name name :verdict :missing})
                         :no-tests
                         (do (println (format "%-20s NO-TESTS" name))
                             {:name name :verdict :no-tests})
                         :no-result
                         (do (println (format "%-20s NO-RESULT  exit=%s (see %s/%s.log)"
                                              name (:exit r) logdir name))
                             {:name name :verdict :fail})
                         (let [bad (and expect (worse? r expect tolerance))
                               better (and expect (better? r expect tolerance))]
                           (println (format "%-20s %-6s %s%s"
                                            name
                                            (cond bad "WORSE" better "BETTER" :else "ok")
                                            (tally-str r)
                                            (if expect
                                              (str "   expected " (tally-str expect))
                                              "   (no recorded expectation)")))
                           {:name name :verdict (cond bad :fail better :better :else :ok)
                            :got (select-keys r [:tests :pass :fail :error :load-fail])}))))))
          bad (filter #(= :fail (:verdict %)) rows)]
      (println)
      (println (format "%d libraries: %d ok, %d better, %d regressed, %d skipped/missing"
                       (count rows)
                       (count (filter #(= :ok (:verdict %)) rows))
                       (count (filter #(= :better (:verdict %)) rows))
                       (count bad)
                       (count (filter #(#{:skip :missing :no-tests} (:verdict %)) rows))))
      (when (seq bad)
        (println "REGRESSED:" (str/join " " (map :name bad))))
      ;; JOLT_LIBCONF_REPORT=<file> dumps {name tally} so the manifest's :expect
      ;; entries can be refreshed from a run instead of retyped.
      (when-let [out (System/getenv "JOLT_LIBCONF_REPORT")]
        (spit out (pr-str (into {} (keep (fn [r] (when (:got r) [(:name r) (:got r)])) rows))))
        (println "wrote" out))
      (System/exit (if (seq bad) 1 0)))))

;; run on load so `jolt run test/conformance/libs/run.clj [lib ...]` executes.
(apply -main *command-line-args*)
