(ns libadd.core
  (:require [jolt.ffi :as ffi]))

(def point-layout (ffi/layout [:struct [[:x :float] [:y :float]]]))

(defn add [x y] (+ x y))
(defn point-size [] (ffi/layout-size point-layout))

;; Publish `add` and `point_size` as C-callable entry points. An embedder
;; resolves them via jolt_lookup("<name>") after jolt_library_init. export! runs
;; at the library's top-level (during heap build), so both are available before
;; jolt_library_init returns.
;;
;; point_size is the one that reaches the Clojure half of jolt.ffi. A library
;; image is not the build driver's image, so a driver that reads its own loaded
;; set as the library's leaves layout-size interned but UNBOUND at the call.
(ffi/export! "add" add [:int :int] :int)
(ffi/export! "point_size" point-size [] :int)

;; gzip_ok answers 1 when a gzip round trip works inside the library (#916):
;; java.util.zip reaches a working zlib, and the bytes are deflated, not stored.
;; zlib-register-smoke.sh pins that the zlib is the library's own.
(defn gzip-ok []
  (let [b (java.io.ByteArrayOutputStream.)]
    (with-open [o (java.util.zip.GZIPOutputStream. b)]
      (.write o (.getBytes (apply str (repeat 1000 "zip")) "UTF-8")))
    (if (and (= 3000 (count (slurp (java.util.zip.GZIPInputStream.
                                     (java.io.ByteArrayInputStream. (.toByteArray b))))))
             (< (.size b) 100))
      1
      0)))
(ffi/export! "gzip_ok" gzip-ok [] :int)

;; alloc_work is the export the release-thread driver calls from a thread the
;; embedder started (#1234). It is :collect-safe because it arrives on a foreign
;; thread, and it allocates on every call so that a loop of them needs a
;; collection: the point at which a parked, still-active init thread used to
;; hold every other thread off for good.
(defn alloc-work [n]
  (let [v (mapv inc (range 256))]
    (+ n (count v))))
(ffi/export! "alloc_work" alloc-work [:int] :int :collect-safe)
